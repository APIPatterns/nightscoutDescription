import "@cadl-lang/rest";
import "@cadl-lang/openapi3";

import "./responses.cadl";
import "./documentCollection.cadl";
import "./models/DeviceStatus.cadl";
import "./models/Entry.cadl";
import "./models/Food.cadl";
import "./models/Treatment.cadl";
import "./models/Profile.cadl";
import "./models/Settings.cadl";
import "./models/Other.cadl";
import "./models/Properties.cadl";

using Cadl.Http;

@service({
    title: "Nightscout API",
    version: "3.0.3"
})
@server("/api","default endpoint")
namespace nightscout {

    @route("v2")
    namespace v2 {
        @route("properties")
        op getProperties(): Properties;

        @route("properties/{properties}")
        op getSelectedProperties(@path properties: string[]): Properties;
    }

    @useAuth(AccessToken | JwtToken)
    @route("v3")
    namespace v3 {
        @route("devicestatus")
        interface deviceStatusDocument extends DocumentCollection<DeviceStatus> {}

        @route("entries")
        interface entryDocument extends DocumentCollection<Entry> {}

        @route("food")
        interface foodDocument extends DocumentCollection<Food> {}

        @route("treatment")
        interface treatmentDocument extends DocumentCollection<Treatment> {}

        @route("profile")
        interface profileDocument extends DocumentCollection<Profile> {}

        @route("settings")
        interface settingsDocument extends DocumentCollection<Settings> {}

        @route("version")
        op getVersion(): OkResult<VersionResult> | UnauthenticatedFailedResponse | UnauthorizedFailedResponse;

        @route("status")
        op getStatus(): OkResult<StatusResult> | UnauthenticatedFailedResponse | UnauthorizedFailedResponse;

        @route("lastmodified")
        op getLastModified(): OkResult<LastModifiedResult> | UnauthenticatedFailedResponse | UnauthorizedFailedResponse;
    }
}

model AccessToken is ApiKeyAuth<ApiKeyLocation.query,"token">;
model JwtToken is BearerAuth;